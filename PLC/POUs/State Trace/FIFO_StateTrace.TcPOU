<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.9">
  <POU Name="FIFO_StateTrace" Id="{3351118f-bba1-4382-8094-400dc0b7365c}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FIFO_StateTrace
VAR_INPUT
	Overwrite: BOOL;
	pFIFO: POINTER TO TraceFIFOBuffer;
	in: StateTimeStamp;
END_VAR

VAR_OUTPUT
	Err		: BOOL;
	Count	: DINT;
	Full	: BOOL;
	out		: StateTimeStamp;
END_VAR

VAR
	Head: UDINT;
	Tail: UDINT;
	Idx: UDINT;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Action Name="INIT" Id="{56528050-524d-446c-8d5e-5ab96d2b6147}">
      <Implementation>
        <ST><![CDATA[Idx := pFIFO^.Size * SIZEOF(in);
MEMSET(ADR(pFIFO^.Buffer[0]), 0, Idx);
MEMSET(ADR(in), 0, SIZEOF(in));
MEMSET(ADR(out), 0, SIZEOF(out));
Count := 0;
Head := 0;
Tail := 0;
Full := 0;
Err := 0;]]></ST>
      </Implementation>
    </Action>
    <Action Name="LOAD" Id="{12824c8a-e31c-4631-a087-ec7f97e93a01}">
      <Implementation>
        <ST><![CDATA[Err := 0;
IF (Full AND Overwrite) OR NOT Full THEN
	pFIFO^.Buffer[Head] := in;
	Head := Head + 1;
	IF Head = pFIFO^.Size THEN
		Head := 0;
	END_IF
	IF Full THEN
		Tail := Tail + 1;
		IF Tail = pFIFO^.Size THEN
			Tail := 0;
		END_IF
	ELSE
		Count := Count + 1;
	END_IF
ELSE
	Err := 1;
END_IF
Full := (Head = Tail);]]></ST>
      </Implementation>
    </Action>
    <Action Name="PEEK" Id="{83ba5d0a-95e4-4f9c-9db2-b00842a60543}">
      <Implementation>
        <ST><![CDATA[Err := 0;
IF Full OR (Tail <> Head) THEN
	out := pFIFO^.Buffer[Tail];
ELSE
	Err := 1; (*fifo is empty*)
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="UNLOAD" Id="{49c0b6ca-fa7d-4c3c-8de4-3e5aac03aa31}">
      <Implementation>
        <ST><![CDATA[Err := 0;
IF NOT Full AND (Head = Tail) THEN
	(*empty*)
	Err := 1;
ELSE
	out := pFIFO^.Buffer[Tail];
	MEMSET(ADR(pFIFO^.Buffer[Tail]), 0, SIZEOF (in));
	Tail := Tail + 1;
	Count := Count - 1;
	IF Tail = pFIFO^.Size THEN
		Tail := 0;
	END_IF
	Full := 0;
END_IF]]></ST>
      </Implementation>
    </Action>
    <LineIds Name="FIFO_StateTrace">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FIFO_StateTrace.INIT">
      <LineId Id="2" Count="7" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FIFO_StateTrace.LOAD">
      <LineId Id="2" Count="17" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FIFO_StateTrace.PEEK">
      <LineId Id="2" Count="4" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FIFO_StateTrace.UNLOAD">
      <LineId Id="2" Count="12" />
      <LineId Id="1" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>
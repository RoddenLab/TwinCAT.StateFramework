<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.9">
  <POU Name="FIFO_LREAL100" Id="{80f8a9bc-3c38-44b7-9048-828fd7078f0c}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FIFO_LREAL100
VAR_INPUT
	Overwrite: BOOL;
	pFIFO: POINTER TO LREAL100FIFOBuffer;
	in: LREAL;
END_VAR

VAR_OUTPUT
	Err: BOOL;
	Count: DINT;
	Full: BOOL;
	out: LREAL;
END_VAR

VAR
	Head: UDINT;
	Tail: UDINT;
	Idx: UDINT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Action Name="INIT" Id="{4ba445f1-6cb3-4c59-9cd6-5fe6213ca4ac}">
      <Implementation>
        <ST><![CDATA[Idx := pFIFO^.Size * SIZEOF(in);
MEMSET(ADR(pFIFO^.Buffer[0]), 0, Idx);
MEMSET(ADR(in), 0, SIZEOF(in));
MEMSET(ADR(out), 0, SIZEOF(out));
Count := 0;
Head := 0;
Tail := 0;
Full := 0;
Err := 0;]]></ST>
      </Implementation>
    </Action>
    <Action Name="LOAD" Id="{f3d896ba-3a72-4287-9bb3-2c8e29a90546}">
      <Implementation>
        <ST><![CDATA[Err := 0;
IF (Full AND Overwrite) OR NOT Full THEN
	pFIFO^.Buffer[Head] := in;
	Head := Head + 1;
	IF Head = pFIFO^.Size THEN
		Head := 0;
	END_IF
	IF Full THEN
		Tail := Tail + 1;
		IF Tail = pFIFO^.Size THEN
			Tail := 0;
		END_IF
	ELSE
		Count := Count + 1;
	END_IF
ELSE
	Err := 1;
END_IF
Full := (Head = Tail);]]></ST>
      </Implementation>
    </Action>
    <Action Name="PEEK" Id="{5179aac3-748a-4b1d-a9ff-8fcfb8c95590}">
      <Implementation>
        <ST><![CDATA[Err := 0;
IF Full OR (Tail <> Head) THEN
	out := pFIFO^.Buffer[Tail];
ELSE
	Err := 1; (*fifo is empty*)
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="UNLOAD" Id="{b5d99004-0e04-42f3-a418-8968ce2607b7}">
      <Implementation>
        <ST><![CDATA[Err := 0;
IF NOT Full AND (Head = Tail) THEN
	(*empty*)
	Err := 1;
ELSE
	out := pFIFO^.Buffer[Tail];
	MEMSET(ADR(pFIFO^.Buffer[Tail]), 0, SIZEOF (in));
	Tail := Tail + 1;
	Count := Count - 1;
	IF Tail = pFIFO^.Size THEN
		Tail := 0;
	END_IF
	Full := 0;
END_IF]]></ST>
      </Implementation>
    </Action>
    <LineIds Name="FIFO_LREAL100">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FIFO_LREAL100.INIT">
      <LineId Id="2" Count="7" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FIFO_LREAL100.LOAD">
      <LineId Id="2" Count="17" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FIFO_LREAL100.PEEK">
      <LineId Id="2" Count="4" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FIFO_LREAL100.UNLOAD">
      <LineId Id="2" Count="12" />
      <LineId Id="1" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>